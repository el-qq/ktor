apply plugin: 'binary-compatibility-validator'

apiValidation {
    def excludeList = [
        'ktor-serialization-kotlinx-tests',
    ].toSet()


    def projects = [].toSet()
    def queue = [
        project(":ktor-http"),
        project(":ktor-network"),
        project(":ktor-utils"),
        project(":ktor-shared"),
    ]

    while (!queue.isEmpty()) {
        def project = queue.remove(queue.size() - 1) as Project
        if (projects.add(project)) {
            queue += project.childProjects.values()
        }
    }

    def projectNames = projects.collect { it.name }.toSet()

    ignoredProjects += excludeList
    ignoredProjects += project.allprojects.collect { it.name }.findAll { !projectNames.contains(it) }
}
